package(default_visibility=["//visibility:public"])

load("//tools:sass_bundle.bzl", "sass_bundle")
load("//tools:defaults.bzl", "ng_module", "ng_package")
load("//:packages.bzl", "MDL_PACKAGES", "MDL_TARGETS", "MDL_SCSS_LIBS",
    "ROLLUP_GLOBALS")

# Root "@angular/mdl" entry-point.
ng_module(
  name = "core",
  srcs = glob(["*.ts"], exclude=["**/*.spec.ts"]),
  module_name = "@angular/mdl",
  deps = ["//src/lib/%s" % p for p in MDL_PACKAGES] + [
    "@npm//@angular/forms",
  ],
)

# Alias so that developers can build Core using it's label shorthand (e.g. "build //src/lib")
alias(
  name = "lib",
  actual = "core",
)

sass_bundle(
  name = "theming_bundle",
  srcs = MDL_SCSS_LIBS,
  entry_point = '//src/lib/common:theming/_all-theme.scss',
  output_name = "_theming.scss",
)

filegroup(
  name = "overviews",
  srcs = ["//src/lib/%s:overview" % name for name in MDL_PACKAGES]
)

# Creates the `@angular/mdl` package published to npm.
ng_package(
  name = "npm_package",
  srcs = ["package.json"],
  entry_point = "src/lib/public_api.js",
  entry_point_name = "core",
  globals = ROLLUP_GLOBALS,
  data = [
    ":theming_bundle",
    "//src/lib/common:theming/prebuilt/indigo-pink",
    "//src/lib/common:theming/prebuilt/deeppurple-amber",
    "//src/lib/common:theming/prebuilt/pink-bluegrey",
    "//src/lib/common:theming/prebuilt/purple-green",
  ],
  packages = ["//src/lib/schematics:npm_package"],
  deps = MDL_TARGETS,
  # TODO(devversion): Use the npm package for publishing. Right now this is disabled because
  # we build using AOT for serving & testing, but the `ng_package` rule should not include factory
  # files.
  tags = ["manual"],
)
